# Kubernetes Deployment with schema validation
#
# Demonstrates compile-time validation of K8s manifests using the
# Hone schema library. Try introducing errors to see what gets caught:
#   - Change `replicas` to a string: `replicas: "3"` -> type error
#   - Misspell a field: `replicsa: 3` -> unknown field error
#   - Remove `containers` from spec -> missing required field
#   - Set port to 99999 -> no error (port is unranged int in K8s schema)
#
# Run:
#   hone compile examples/k8s-validated/deployment.hone --format yaml

import "../../lib/k8s/v1.30/apps.hone" as apps
import "../../lib/k8s/v1.30/core.hone" as core
import "../../lib/k8s/v1.30/_meta.hone" as meta

use Deployment

let app_name = "my-api"
let image_tag = "v1.2.0"

apiVersion: "apps/v1"
kind: "Deployment"
metadata {
  name: app_name
  namespace: "default"
  labels {
    "app.kubernetes.io/name": app_name
    "app.kubernetes.io/version": image_tag
  }
}
spec {
  replicas: 3
  selector {
    matchLabels {
      "app.kubernetes.io/name": app_name
    }
  }
  template {
    metadata {
      labels {
        "app.kubernetes.io/name": app_name
      }
    }
    spec {
      containers: [{
        name: app_name,
        image: "ghcr.io/myorg/${app_name}:${image_tag}",
        ports: [{ containerPort: 8080, protocol: "TCP" }],
        env: [
          { name: "LOG_LEVEL", value: "info" },
          { name: "PORT", value: "8080" },
        ],
        resources {
          requests {
            cpu: "100m"
            memory: "128Mi"
          }
          limits {
            cpu: "500m"
            memory: "512Mi"
          }
        }
        readinessProbe {
          httpGet {
            path: "/healthz"
            port: 8080
          }
          initialDelaySeconds: 5
          periodSeconds: 10
        }
        livenessProbe {
          httpGet {
            path: "/healthz"
            port: 8080
          }
          initialDelaySeconds: 15
          periodSeconds: 20
        }
      }]
    }
  }
}
